configurations {
  arquillianExtension
  testCompile.extendsFrom arquillianExtension

  osgiBundle
  integrationTestOsgiCompile.extendsFrom testCompile
  integrationTestOsgiRuntime
}

apply plugin: 'osgi'

repositories {
  jcenter()
}

dependencies {
  compile "org.jruby:jruby-complete:$jrubyVersion"
  compile "com.beust:jcommander:$jcommanderVersion"
  gems "rubygems:asciidoctor:$asciidoctorGemVersion"
  gems "rubygems:coderay:$coderayGemVersion"
  gems "rubygems:erubis:$erubisGemVersion"
  gems "rubygems:haml:$hamlGemVersion"
  gems "rubygems:open-uri-cached:$openUriCachedGemVersion"
  gems "rubygems:slim:$slimGemVersion"
  gems "rubygems:thread_safe:$threadSafeGemVersion"
  gems "rubygems:tilt:$tiltGemVersion"
  // TODO could use dependency replacement feature to fix version of Saxon-HE
  testCompile("org.xmlmatchers:xml-matchers:$xmlMatchersVersion") { exclude module: 'Saxon-HE' }
  testCompile "net.sf.saxon:Saxon-HE:$saxonVersion"
  testCompile "com.google.guava:guava:$guavaVersion"
  testCompile "org.jsoup:jsoup:$jsoupVersion"
  testCompile "io.netty:netty-all:$nettyVersion"
  testCompile project(path: ':asciidoctorj-test-support', configuration: 'tests')

  arquillianExtension project(path: ':asciidoctorj-test-support', configuration: 'tests')
  arquillianExtension "org.jboss.arquillian.container:arquillian-container-spi:$arquillianVersion"
  arquillianExtension "org.jboss.arquillian.container:arquillian-container-test-spi:$arquillianVersion"
  arquillianExtension "org.jboss.arquillian.container:arquillian-container-impl-base:$arquillianVersion"
  arquillianExtension "org.jboss.arquillian.container:arquillian-container-test-impl-base:$arquillianVersion"

  osgiBundle group: 'org.jboss.arquillian.osgi', name: 'arquillian-osgi-bundle', version: '2.1.0.CR18', transitive: false

  testCompile "org.jboss.arquillian.protocol:arquillian-protocol-jmx:$arquillianVersion"
  testCompile "org.jboss.arquillian.junit:arquillian-junit-container:$arquillianVersion"
  testCompile "org.jboss.arquillian.container:arquillian-container-felix-embedded:2.1.0.CR18"
  testCompile "org.apache.felix:org.apache.felix.framework:5.0.0"
  testCompile "org.apache.felix:org.apache.felix.main:5.0.0"
//  integrationTestOsgiCompile "org.jboss.arquillian.container:arquillian-container-karaf-managed:2.1.0.CR18"
//  integrationTestOsgiCompile "org.osgi:org.osgi.enterprise:5.0.0"
}

def gemFiles = fileTree(jruby.gemInstallDir) {
  include 'specifications/*.gemspec'
  include 'gems/*/lib/**'
  include "gems/asciidoctor-${asciidoctorGemVersion}/data/**"
}

jrubyPrepareGems << {
  copy { // bundles the gems inside this artifact
    from gemFiles
    into sourceSets.main.output.resourcesDir
  }
}

//jruby {
//	execVersion = '1.7.20'
//}

task arquillianExtensionJar(type: Jar, dependsOn: testClasses) {
  baseName = "arquillianExtension-${project.archivesBaseName}"
  from sourceSets.test.output
  include 'org/asciidoctor/arquillian/**/*'
  include 'META-INF/services/*'
}


artifacts {
  arquillianExtension arquillianExtensionJar
}

jar {
  manifest {
    symbolicName = 'org.asciidoctor'
    instruction 'Export-Package',
      "org.asciidoctor;version=\"${version}\"",
      "org.asciidoctor.ast;version=\"${version}\"",
      "org.asciidoctor.converter;version=\"${version}\"",
      "org.asciidoctor.converter.spi;version=\"${version}\"",
      "org.asciidoctor.extension;version=\"${version}\"",
      "org.asciidoctor.extension.spi;version=\"${version}\""
    instruction 'Import-Package',
      'com.beust.jcommander;resolution:=optional',
      '*'
  }
}

sourceSets {
  integrationTestOsgi {
    java {
      srcDir file('src/integration-test-osgi/java')
      compileClasspath += main.output + test.output + configurations.integrationTestOsgiCompile
    }
    resources.srcDir file('src/integration-test-osgi/resources')
  }
}

task copyOsgiBundle(type: Copy) {
  from configurations.osgiBundle
  into sourceSets.integrationTestOsgi.output.resourcesDir
}

task integrationTestOsgi(type: Test) {
  println sourceSets.integrationTestOsgi.runtimeClasspath.asPath
  testClassesDir = sourceSets.integrationTestOsgi.output.classesDir
  classpath = sourceSets.integrationTestOsgi.runtimeClasspath
  dependsOn copyOsgiBundle
  dependsOn jar
}

check.dependsOn integrationTestOsgi
