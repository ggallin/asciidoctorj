dependencies {
  compile project(':asciidoctorj')

  testCompile project(path: ':asciidoctorj-arquillian-extension')

  gems("rubygems:asciidoctor-pdf:$asciidoctorPdfGemVersion") {
    // Exclude gems provided by AsciidoctorJ core
    exclude module: 'asciidoctor'
    exclude module: 'thread_safe'
    // FIXME The prawn and ttfunk gems are not resolving correctly, so we have to do it manually for now
    exclude module: 'prawn'
    exclude module: 'ttfunk'
  }
  gems "rubygems:prawn:$prawnGemVersion"
  gems "rubygems:ttfunk:$ttfunkGemVersion"
}

def gemFiles = fileTree(jruby.gemInstallDir) {
  include 'specifications/*.gemspec'
  include 'gems/*/lib/**'
  include "gems/*/data/fonts/*"
  include "gems/asciidoctor-pdf-${asciidoctorPdfGemVersion}/data/**"
  // Accomodate Prawn's non-conforming packaging
  include "gems/prawn-$prawnGemVersion/VERSION"
  include "gems/prawn-$prawnGemVersion/data/*.txt"
  include "gems/prawn-$prawnGemVersion/data/encodings/*"
  // Include Font Awesome fonts
  include "gems/prawn-icon-*/fonts/fa/*"
  // Accomodate Addressable's non-conforming packaging
  include "gems/addressable-*/data/*.data"
}

jrubyPrepareGems << {
  copy { // bundles the gems inside this artifact
    from gemFiles
    into sourceSets.main.output.resourcesDir
  }
}
